apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    ndkVersion "21.3.6528147"
    defaultConfig {
        applicationId "unzen.android.test.cpp.exec"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode baseVersion
        versionName "$baseVersion"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.debug
        buildConfigField("int", "VERSION_CODE_BASE", "$baseVersion")
    }
    buildTypes {
        debug {
            versionNameSuffix "D"
            minifyEnabled false
        }
        release {
            versionNameSuffix "R"
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "abi"

    productFlavors {
        x64 {
            versionCode 4 + android.defaultConfig.versionCode
            versionName "$versionCode+X64"
            ndk {
                abiFilters "x86_64"
            }
            ext {
                abis = ["x86_64"]
            }
        }
        x32 {
            versionCode 3 + android.defaultConfig.versionCode
            versionName "$versionCode+X32"
            ndk {
                abiFilters "x86"
            }
            ext {
                abis = ["x86"]
            }
        }
        a64 {
            versionCode 2 + android.defaultConfig.versionCode
            versionName "$versionCode+A64"
            ndk {
                abiFilters "arm64-v8a"
            }
            ext {
                abis = ["arm64-v8a"]
            }
        }
        a32 {
            versionCode 1 + android.defaultConfig.versionCode
            versionName "$versionCode+A32"
            ndk {
                abiFilters "armeabi-v7a"
            }
            ext {
                abis = ["armeabi-v7a"]
            }
        }
        fat {
            versionCode android.defaultConfig.versionCode
            versionName "$versionCode+FAT"
            ndk {
                abiFilters = baseAbis
            }
            ext {
                abis = baseAbis
            }
        }
    }
    variantFilter { variant ->
        def names = variant.flavors*.name
        if (variant.buildType.name != "release" && !names.contains("fat")) {
            setIgnore(true)
        }
    }
}

android.applicationVariants.all { variant ->
    def srcAssetsDirPath = 'execbarbaz'
    def taskSuffix = variant.name.capitalize()
    def defMergeAssetsTask = project.tasks['merge' + taskSuffix + 'Assets']

    // Prepare input dir path
    def execModuleBuildDirPath = rootDir.absolutePath + "/lib-cppmodule/build"
    def strippedRootDirPath = "$execModuleBuildDirPath/intermediates/stripped_native_libs"
    // <module>/build/intermediates/stripped_native_libs/debug/out/lib
    def strippedVariantDirPath = "$strippedRootDirPath/${variant.getBuildType().name}/out/lib"
    // Prepare output dir path
    def intermedDirPath = "$project.buildDir.absolutePath/intermediates/"
    // <module>/build/intermediates/merged_assets/debug/out/${srcAssetsDirPath}
    def assetsDirPath = "$intermedDirPath/merged_assets/$variant.name/out/$srcAssetsDirPath"

    def toAssetsTaskName = "copyExecToAssets${taskSuffix}"
    def toAssetsTask = tasks.create(name: toAssetsTaskName, type: Copy) {
        from strippedVariantDirPath
        into assetsDirPath
        variant.productFlavors.get(0).abis.each { abi ->
            include abi + '/execbar'
            include abi + '/execbaz'
        }
        includeEmptyDirs = false
    }
    defMergeAssetsTask.finalizedBy toAssetsTask
    defMergeAssetsTask.doLast {
        delete(assetsDirPath)
    }

    variant.outputs.all { output ->
        outputFileName = "cppexec-${variant.versionName}.apk"
    }
}

gradle.taskGraph.afterTask { task ->
    // Prints all tasks info, inputs and outputs
    if (true) {
        return
    }
    // Allows to print only tasks that match some criteria
    boolean hit = true
    StringBuffer taskInfo = new StringBuffer()
    taskInfo << "Unzen task info:"
    taskInfo << " $task.name, $task.group, $task.description"
    taskInfo << ", $task.convention.plugins"
    task.inputs.files.eachWithIndex { it, i ->
        if (it.absolutePath.contains("java_res")) {
            hit = true
        }
        taskInfo << "\nI: ${it.absolutePath}"
    }
    task.outputs.files.eachWithIndex { it, i ->
        if (it.absolutePath.contains("java_res")) {
            hit = true
        }
        taskInfo << "\nO: ${it.absolutePath}"
    }
    if (hit) {
        println taskInfo
    }
}

task alephCopyApks(type: Copy) {
    group = "aleph"
    from "build/outputs/apk/fat/debug"
    from "build/outputs/apk/fat/release"
    from "build/outputs/apk/a32/release"
    from "build/outputs/apk/a64/release"
    from "build/outputs/apk/x32/release"
    from "build/outputs/apk/x64/release"
    include "*.apk"
    into "$rootDir.absolutePath/apks"
}
assemble.finalizedBy alephCopyApks

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(path: ':lib-cppmodule')
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
}
