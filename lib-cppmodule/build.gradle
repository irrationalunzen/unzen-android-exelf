apply plugin: 'com.android.library'

android {
    compileSdkVersion 29
    ndkVersion "21.3.6528147"
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        externalNativeBuild {
            ndkBuild {
                targets 'jnifoo', 'execbar', 'execbaz'
            }
        }
    }
    buildTypes {
        debug {
            jniDebuggable true
            debuggable = true
            ndk {
                abiFilters = baseAbis
            }
        }
        release {
            jniDebuggable false
            debuggable = false
            ndk {
                abiFilters = baseAbis
            }
        }
    }
    externalNativeBuild {
        ndkBuild {
            path "src/main/cpp/Android.mk"
        }
    }
}

android.libraryVariants.all { variant ->
    def taskSuffix = variant.name.capitalize()
    def defMergeJniTask = project.tasks["merge${taskSuffix}NativeLibs"]
    def defStripTask = project.tasks["strip${taskSuffix}DebugSymbols"]
    def defPackageAssetsTask = project.tasks['package' + taskSuffix + 'Assets']

    def intermediatesDirPath = "$project.buildDir.absolutePath/intermediates"
    // <module>/build/intermediates/ndkBuild/debug/obj/local
    def objDirPath = "$intermediatesDirPath/ndkBuild/$variant.dirName/obj/local"
    // <module>/build/intermediates/merged_native_libs/debug/out/lib
    def mergedDirPath = "$intermediatesDirPath/merged_native_libs/$variant.dirName/out/lib"

    def prepareForStripTaskName = "prepareExecForStrip${taskSuffix}"
    def prepareForStripTask = tasks.create(name: prepareForStripTaskName, type: Copy) {
        from objDirPath
        into mergedDirPath
        baseAbis.each { abi ->
            include abi + '/execbar'
            include abi + '/execbaz'
        }
        includeEmptyDirs = false
    }
    // Without dependency defMergeJniTask will overwrite our work in mergedDirPath
    prepareForStripTask.dependsOn defMergeJniTask
    defStripTask.dependsOn prepareForStripTask
    defPackageAssetsTask.dependsOn defStripTask
}

task cppVersions {
    File header = file('src/main/cpp/global_version.h')
    inputs.property('globalVersion', baseVersion)
    outputs.file header
    doLast {
        String template = "#ifndef %s%n#define %s%n%n#define %s \"%s\"%n%n#endif // %s"
        def name = "GLOBAL_VERSION"
        def guard = "_GLOBAS_VERSION_H_"
        def version = 'UNZEN-VERSION-' + baseVersion
        header.write(String.format(template, guard, guard, name, version, guard))
    }
}
preBuild.dependsOn cppVersions
